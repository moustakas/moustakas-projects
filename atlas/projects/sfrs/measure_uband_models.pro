;+
; NAME:
;       MEASURE_UBAND_MODELS
;
; PURPOSE:
;       Measure quantities of interest from the models generated by
;       UBAND_MODELS.  
;
; CALLING SEQUENCE:
;
; INPUTS:
;
; OPTIONAL INPUTS:
;
; KEYWORD PARAMETERS:
;
; OUTPUTS:
;
; OPTIONAL OUTPUTS:
;
; PROCEDURES USED:
;
; COMMENTS:
;
; EXAMPLES:
;
; MODIFICATION HISTORY:
;       J. Moustakas, 2005 Mar 17, U of A
;       jm05apr12uofa - added birthrate parameter measurements  
;-

pro measure_uband_models, write=write

    diskage = 10.0 ; disk/galaxy age [Gyr] <-- MUST MATCH IN UBAND_MODELS

    datapath = atlas_path(/projects)+'sfrs/uband/'
    Uinfo = im_filterspecs(filterlist='bessell_U.par')

    vegadist = 7.756*3.085678D18 ; distance to vega [cm]
    Uconst = Uinfo.weff*Uinfo.vega_flam*(4*!dpi*vegadist^2)

    hahb = return_tbalmer()
    sfrhaconst = 7.9D-42
    light = 2.99792458D5        ; speed of light [km/s]
    lsun = 3.826D33             ; [erg/s]
    fwhm2sig = 2D0*sqrt(2.0*alog(2.0))
    
    linewidth = 100.0 ; intrinsic line-width [km/s]

    balmer = balmer_data()
    balmerwaves = balmer.wave
    fwhm = 3.0
    
    balmersigma = sqrt((balmerwaves*linewidth/light)^2 + (fwhm/fwhm2sig)^2)

; initialize the parameters used in UBAND_MODELS

    tau = [2.0,3.0,5.0,8.0]
    isedcontfile = 'uband_'+['tau2','tau3','tau5','tau8']+'.ised'
    ncont = n_elements(isedcontfile)

    tburst = [1.0,2.0,5.0]             ; time *since* burst [Gyr]
;   tburst = [1.0,2.0,3.0,5.0]         ; time *since* burst [Gyr]
;   tburst = [1.0,2.0,3.0,4.0,5.0,6.0] ; time *since* burst [Gyr]
    ageatburst = diskage - tburst      ; age of the galaxy at the time of the burst [Gyr]
    nburst = n_elements(tburst)

    tburst_string = strtrim(string(tburst,format='(F4.1)'),2)
    ageatburst_string = strtrim(string(ageatburst,format='(F4.1)'),2)

; measure the continuous star formation model at these ages [Myr]    
    
    constantage = [10.0,100.0,500.0,10000.0]*1E6 
    nage = n_elements(constantage)
    
; initialize the output data structure

    result = {$
      isedfile_cont:    '',           $
      tau:             0.0,           $
      sfr_cont:        0.0,           $
      ewha_cont:       0.0,           $ ; EW(Ha)
      ewhb_cont:       0.0,           $ ; EW(Hb)
      LU_cont:         0.0D,          $
      LHa_cont:        0.0D,          $
      LHb_cont:        0.0D,          $
      D4000_cont:      0.0,           $
      birthrate_cont:  0.0,           $ ; birthrate parameter
      fraction_cont:   0.0,           $ ; fraction of gas returned to the ISM

      isedfile_burst:  strarr(nburst),$
      tburst:                  tburst,$
      sfr_burst:       fltarr(nburst),$
      ewha_burst:      fltarr(nburst),$
      ewhb_burst:      fltarr(nburst),$
      LU_burst:        dblarr(nburst),$
      LHa_burst:       dblarr(nburst),$
      LHb_burst:       dblarr(nburst),$
      D4000_burst:     fltarr(nburst),$
      birthrate_burst: fltarr(nburst),$
      fraction_burst:  fltarr(nburst)}
    result = replicate(result,ncont)
    result.tau = tau
    result.isedfile_cont = isedcontfile
    
    result_constant = {$
      isedfile:   '',  $
      age:       0.0,  $
      sfr:       0.0,  $
      ewha:      0.0,  $
      ewhb:      0.0,  $
      LU:        0.0D, $
      LHa:       0.0D, $
      LHb:       0.0D, $
      D4000:     0.0,  $
      birthrate: 0.0,  $
      fraction:  0.0}
    result_constant = replicate(result_constant,nage)
    result_constant.age = constantage

; ###########################################################################
; read the continuous SFH (tau) models
; ###########################################################################

    splog, 'Reading the continuous models.'
    for icont = 0L, ncont-1L do begin
    
       cont = im_read_bc03(isedfile=isedcontfile[icont],isedpath=datapath,$
         /salpeter,minwave=3000,maxwave=9000,bc03_extras=continfo,/silent)

       get_element, continfo.logage, alog10(diskage*1E9), ageindx

       result[icont].sfr_cont = continfo[ageindx].sfr_yr               ; [M_sun/yr]
       result[icont].LU_cont = Uconst*10^(-0.4*continfo[ageindx].Umag) ; [erg/s]
       result[icont].LHa_cont = continfo[ageindx].sfr_yr/sfrhaconst    ; [erg/s]
       result[icont].LHb_cont = result[icont].LHa_cont/hahb            ; [erg/s]
       result[icont].D4000_cont = continfo[ageindx].b4_vn

; measure the continuum around Ha and Hb to estimate the EW 
       
       balmerabs = ibalmerabs(cont.wave,cont.flux[*,ageindx],$
         ferr=cont.flux[*,ageindx]*0.1,balmersigma=balmersigma,$
         balmerwaves=balmerwaves,debug=debug)

       result[icont].ewha_cont = result[icont].LHa_cont/(lsun*balmerabs[3].babs_continuum) ; [Angstrom]
       result[icont].ewhb_cont = result[icont].LHb_cont/(lsun*balmerabs[2].babs_continuum) ; [Angstrom]

; compute the fraction of gas returned to the ISM and the birthrate
; parameter 
       
       result[icont].fraction_cont = 1.0-continfo[ageindx].mgas/continfo[ageindx].mgalaxy
       result[icont].birthrate_cont = continfo[ageindx].sfr_yr*result[icont].fraction_cont*$
         diskage*1E9/continfo[ageindx].m_
       
    endfor

; ###########################################################################
; read the constant SFR model; compute the L(U)/L(Ha) ratio for the
; constant SFR model at various ages to try to reproduce the dwarfs
; ###########################################################################

    isedconstantfile = 'uband_constant.ised'
    constant = im_read_bc03(isedfile=isedconstantfile,isedpath=datapath,$
      /salpeter,minwave=3000,maxwave=9000,bc03_extras=constantinfo,/silent)

    get_element, constantinfo.logage, alog10(constantage), ageindx

    result_constant.isedfile = isedconstantfile
    result_constant.sfr = constantinfo[ageindx].sfr_yr              ; [M_sun/yr]
    result_constant.LU = Uconst*10^(-0.4*constantinfo[ageindx].Umag) ; [erg/s]
    result_constant.LHa = constantinfo[ageindx].sfr_yr/sfrhaconst   ; [erg/s]
    result_constant.LHb = result_constant.LHa/hahb                  ; [erg/s]
    result_constant.D4000 = constantinfo[ageindx].b4_vn
    
; measure the continuum around Ha and Hb to estimate the EW for each
; age

    for iage = 0L, nage-1L do begin
    
       balmerabs = ibalmerabs(constant.wave,constant.flux[*,ageindx[iage]],$
         ferr=constant.flux[*,ageindx[iage]]*0.1,balmersigma=balmersigma,$
         balmerwaves=balmerwaves,debug=debug)

       result_constant[iage].ewha = result_constant[iage].LHa/(lsun*balmerabs[3].babs_continuum)
       result_constant[iage].ewhb = result_constant[iage].LHb/(lsun*balmerabs[2].babs_continuum)

    endfor

; compute the fraction of gas returned to the ISM and the birthrate
; parameter 

    result_constant.fraction = 1.0-constantinfo[ageindx].mgas/constantinfo[ageindx].mgalaxy
    result_constant.birthrate = constantinfo[ageindx].sfr_yr*result_constant.fraction*$
      diskage*1E9/constantinfo[ageindx].m_

; ###########################################################################
; next, loop on each continuous plus burst models
; ###########################################################################

    for icont = 0L, ncont-1L do begin
    
       for iburst = 0L, nburst-1L do begin

          print, format='("Base model ",I0,"/",I0,", Burst ",I0,"/",I0,".",A1,$)', $
            icont+1L, ncont, iburst+1L, nburst, string(13b)

          isedfile = repstr(isedcontfile[icont],'.ised','_burst_500Myr_'+$
            tburst_string[iburst])+'GyrAgo.ised'

          burst1 = im_read_bc03(isedfile=isedfile,isedpath=datapath,$
            /salpeter,minwave=3000,maxwave=9000,bc03_extras=burstinfo,/silent)

          get_element, burstinfo.logage, alog10(diskage*1E9), ageindx
          
          result[icont].isedfile_burst[iburst] = isedfile
          result[icont].sfr_burst[iburst] = burstinfo[ageindx].sfr_yr               ; [M_sun/yr]
          result[icont].LU_burst[iburst] = Uconst*10^(-0.4*burstinfo[ageindx].Umag) ; [erg/s]
          result[icont].LHa_burst[iburst] = burstinfo[ageindx].sfr_yr/sfrhaconst    ; [erg/s]
          result[icont].LHb_burst[iburst] = result[icont].LHa_burst[iburst]/hahb    ; [erg/s]
          result[icont].D4000_burst[iburst] = burstinfo[ageindx].b4_vn

; measure the continuum around Ha and Hb to estimate the EW 
          
          balmerabs = ibalmerabs(burst1.wave,burst1.flux[*,ageindx],$
            ferr=burst1.flux[*,ageindx]*0.1,balmersigma=balmersigma,$
            balmerwaves=balmerwaves,debug=debug)

          result[icont].ewha_burst[iburst] = result[icont].LHa_burst[iburst]/(lsun*balmerabs[3].babs_continuum)
          result[icont].ewhb_burst[iburst] = result[icont].LHb_burst[iburst]/(lsun*balmerabs[2].babs_continuum)

; compute the fraction of gas returned to the ISM and the birthrate
; parameter 
       
          result[icont].fraction_burst[iburst] = 1.0-burstinfo[ageindx].mgas/burstinfo[ageindx].mgalaxy
          result[icont].birthrate_burst[iburst] = burstinfo[ageindx].sfr_yr*result[icont].fraction_burst[iburst]*$
            diskage*1E9/burstinfo[ageindx].m_

       endfor
          
    endfor

; write out

    if keyword_set(write) then begin
       outfile = 'measure_uband_models.fits'
       mwrfits, result, datapath+outfile, /create
       mwrfits, result_constant, datapath+outfile
    endif

stop    
    
return
end
    
