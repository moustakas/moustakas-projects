pro mass_05savaglio, phot, result, band_shift=band_shift, write=write
; jm06oct24nyu - derive stellar masses for the GDDS
; jm06nov19nyu - rewritten based on AGES_KCORRECT
    
; define the cosmology and some constants

    if (n_elements(band_shift) eq 0L) then band_shift = 0.0

    red, omega0=0.3, omegalambda=0.7, h100=0.70
    omega0 = redomega0() & omegal = redomegal() & h100 = redh100()

    imf_chabrier_to_salpeter = +0.25

    vname = 'default.nolines'   ; NOTE!
    
; read the photometry    
    
    path = getenv('CATALOGS_DIR')+'/05savaglio/'
;   sava = rsex(path+'05savaglio.dat')

    if (n_elements(phot) eq 0L) then begin
       
       phot1 = im_read_fmr(path+'04abraham_table4.dat')
       goodphot1 = where(phot1.z gt 0.0,ngalaxy)

       phot = {galaxy: '', z: 0.0, $
         phot_b: -999.0, phot_b_err: -999.0, $
         phot_v: -999.0, phot_v_err: -999.0, $
         phot_r: -999.0, phot_r_err: -999.0, $
         phot_i: -999.0, phot_i_err: -999.0, $
         phot_z: -999.0, phot_z_err: -999.0, $
         phot_h: -999.0, phot_h_err: -999.0, $
         phot_k: -999.0, phot_k_err: -999.0}
       phot = replicate(phot,ngalaxy)

       phot.galaxy = phot1[goodphot1].id
       phot.z      = phot1[goodphot1].z

       phot.phot_b = phot1[goodphot1].bmag & phot.phot_b_err = phot1[goodphot1].ebmag
       phot.phot_v = phot1[goodphot1].vmag & phot.phot_v_err = phot1[goodphot1].evmag 
       phot.phot_r = phot1[goodphot1].rmag & phot.phot_r_err = phot1[goodphot1].ermag
       phot.phot_i = phot1[goodphot1].imag & phot.phot_i_err = phot1[goodphot1].eimag
       phot.phot_z = phot1[goodphot1].zmag & phot.phot_z_err = phot1[goodphot1].ezmag
       phot.phot_h = phot1[goodphot1].hmag & phot.phot_h_err = phot1[goodphot1].ehmag
       phot.phot_k = phot1[goodphot1].kmag & phot.phot_k_err = phot1[goodphot1].ekmag

; replace upper limits with "no detection"; two objects have zero
; R-band magnitude error: set this equal to 0.01
       
       for i = 0L, ngalaxy-1L do begin
          for j = 0L, n_tags(phot[0])-1L do begin
             if (size(phot[i].(j),/type) ne 7L) then begin
                if (phot[i].(j) eq -9.99) then begin
                   phot[i].(j) = -999.0 ; magnitude error
                   phot[i].(j-1) = -999.0 ; magnitude
                endif
             endif
          endfor
       endfor

       zero = where(phot.phot_r_err eq 0.0)
       phot[zero].phot_r_err = 0.01

    endif
       
; define the rest-frame filters of interest

    splog, 'Loading rest-frame filters:'
;   restfilters = ['bessell_'+['B','V','R'],'sdss_'+['u0','g0','r0'],'twomass_'+['J']]+'.par'
;   restbandpasses = ['B','V','R','sdss_u','sdss_g','sdss_r','J']
;   restvega = [1,1,1,0,0,0,1]
    restfilters = ['bessell_'+['B','V','R'],'twomass_'+['J']]+'.par'
    restbandpasses = ['B','V','R','J']
    restvega = [1,1,1,1]
    nrestfilter = n_elements(restfilters)

    restvega2ab = k_vega2ab(filterlist=restfilters,/kurucz)
    notvega = where((restvega eq 0L),nnotvega)
    if (nnotvega ne 0L) then restvega2ab[notvega] = 0.0
    
; define the filters (NDWFS/SDSS/FLAMEX) for which we have
; observed-frame photometry 

    splog, 'Defining filter functions with observed photometry:'
    obsfilters = 'gdds_'+['B','V','R','I','z','H','K']+'.par'
    obsbands = 'phot_'+['b','v','r','i','z','h','k']
    obsvega2ab = k_vega2ab(filterlist=obsfilters,/kurucz)
    nobsfilter = n_elements(obsfilters)

; convert the observed photometry to maggies and store

    splog, 'Converting observed photometry to maggies.'
    obsmaggies = convert_to_maggies(phot,obsbands,obsvega2ab,$
      maggies_invvar=obsmaggies_ivar,mags=mobs_ab,err_mags=mobs_ab_err)
    setzero = where(obsmaggies_ivar eq 1D-32,nsetzero)
    if (nsetzero ne 0L) then obsmaggies_ivar[setzero] = 0.0

    redshift = phot.z

; compute the k-corrections      

    splog, 'Fitting k-correct templates.'
    t0 = systime(1)
    kcorrect, obsmaggies, obsmaggies_ivar, redshift, first_kcorrect, $
      band_shift=band_shift, filterlist=obsfilters, filterpath=filters_path, $
      rmatrix=rmatrix, zvals=zvals, lambda=lambda, vmatrix=vmatrix, $
      coeffs=coeffs, rmaggies=obsmaggies_synth, chi2=chi2, maxiter=maxiter, omega0=omega0, $
      omegal0=omegal, mass=mass, intsfh=intsfh, mets=mets, b300=b300, $
      b1000=b1000, mtol=mtol, vname=vname, /silent
    splog, 'Total time = '+string((systime(1)-t0)/60.0,format='(G0.0)')+' minutes.'

; generate the maggies lookup table; only works to z=2 (ie, not for
; all quasars!) and then compute k-corrections
    
    splog, 'Computing k-corrections.'

    k_projection_table, rmatrix, vmatrix, lambda, zvals, restfilters, $ 
      zmin=zmin, zmax=zmax, nz=nz, filterpath=filters_path, /silent

    k_reconstruct_maggies, coeffs, replicate(band_shift,ngalaxy), $
      restmaggies_synth, rmatrix=rmatrix, zvals=zvals
    restmaggies_synth = restmaggies_synth/(1.0+band_shift)

; figure out the bandpass that minimizes the k-correction 
    
    obands = lindgen(nrestfilter) # replicate(1L,ngalaxy) ; index array

    lambda_in = k_lambda_eff(filterlist=obsfilters)
    lambda_out = k_lambda_eff(filterlist=restfilters,band_shift=band_shift)

    for i = 0L, ngalaxy-1L do begin
       for j = 0L, nrestfilter-1L do begin
          dmin = min(abs(lambda_in/(1.0+redshift[i])-lambda_out[j]),imin)
          obands[j,i]= imin
       endfor
    endfor

    kcorrect = fltarr(nrestfilter,ngalaxy)
    for i = 0L, ngalaxy-1L do for j = 0L, nrestfilter-1L do $
      kcorrect[j,i] = restmaggies_synth[j,i]/obsmaggies_synth[obands[j,i],i]
    kcorrect = 2.5*alog10(kcorrect)

; calculate rest-frame magnitudes using the observed photometry when
; available, otherwise the synthesized photometry (should be identical
; when the fit is good); alternatively, we could find the next nearest
; bandpass to compute the k-correction

    mrest_ab = -2.5*alog10(restmaggies_synth)
    mrest_ab_ivar = mrest_ab*0.0

;   absmag = mrest_ab*0.0
;   for i = 0L, nrestfilter-1L do absmag[i,*] = -2.5*alog10(restmaggies_synth[i,*]) - $ ; test
;     lf_distmod(redshift,omega0=omega0,omegal0=omegal0)-5.0*alog10(1.0/h100) 
    
    for j = 0L, ngalaxy-1L do begin

       igood = where((obsmaggies_ivar[obands[*,j],j] gt 0.0) and (obsmaggies[obands[*,j],j] gt 0.0),ngood)

       if (ngood gt 0L) then begin
          mrest_ab[igood,j] = -2.5*alog10(obsmaggies[obands[igood,j],j]) - kcorrect[igood,j]
;         absmag[igood,j] = -2.5*alog10(obsmaggies[obands[igood,j],j])-lf_distmod(redshift[j],$ ; test
;           omega0=omega0,omegal0=omegal0)-5.0*alog10(1.0/h100)-kcorrect[igood,j]
          mrest_ab_ivar[igood,j] = obsmaggies[obands[igood,j],j]^2.0 * $
            obsmaggies_ivar[obands[igood,j],j]*(0.4*alog(10.0))^2.0
       endif

    endfor

; initialize the output data structure

    result_template = {$
      galaxy:                                     '', $
      z:                                      -999.0, $
      kcorr_mass:                             -999.0, $
      kcorr_mets:                             -999.0, $
      kcorr_intsfh:                           -999.0, $
      kcorr_coeffs:                        fltarr(5), $  ; eigentemplate coefficients
      kcorr_chi2:                             -999.0, $ ; reduced chi2
      kcorr_zzmax:                            -999.0, $ ; compute z/zmax (see below)
      kcorr_kcorr:         fltarr(nrestfilter)-999.0, $ ; k-correction
      kcorr_mobs_ab:        fltarr(nobsfilter)-999.0, $ ; "actual observed photometry"
      kcorr_mobs_ab_err:    fltarr(nobsfilter)-999.0, $ ; error in MOBS_AB
      kcorr_mobs_ab_synth:        fltarr(nobsfilter), $ ; "synthesized observed photometry"
      kcorr_mrest:         fltarr(nrestfilter)-999.0, $ ; k-corrected, rest frame, using "actual observed photometry"
      kcorr_mrest_err:     fltarr(nrestfilter)-999.0, $ ; error in MREST
      kcorr_mrest_synth:         fltarr(nrestfilter)}   ; synthesized, rest frame

    for i = 0L, nrestfilter-1L do result_template = create_struct(result_template,'M_'+restbandpasses[i],-999.0)

    result = replicate(result_template,ngalaxy)
    
; store all the results; convert the stellar mass to the Salpeter IMF
; and to h=0.7

    result.galaxy            = phot.galaxy
    result.z                 = phot.z
    result.kcorr_mobs_ab     = mobs_ab
    result.kcorr_mobs_ab_err = mobs_ab_err

    good = where((mass gt 1E6) and finite(mass),ngood)
    if (ngood ne 0L) then begin

       result[good].kcorr_mass = alog10(mass[good]) - alog10(h100) + imf_chabrier_to_salpeter
       result[good].kcorr_mets = mets[good]
       result[good].kcorr_intsfh = alog10(intsfh[good])
       result[good].kcorr_coeffs = coeffs[*,good]

       result[good].kcorr_kcorr = kcorrect[*,good]
       result[good].kcorr_mobs_ab_synth = -2.5*alog10(obsmaggies_synth[*,good])

       result[good].kcorr_mrest = mrest_ab[*,good] - rebin(restvega2ab,nrestfilter,ngood)
       result[good].kcorr_mrest_synth = -2.5*alog10(restmaggies_synth[*,good]) - rebin(restvega2ab,nrestfilter,ngood)

; compute absolute magnitudes       
       
       for iband = 0L, nrestfilter-1L do begin
          true = tag_exist(result,'M_'+restbandpasses[iband],index=bandindx)
          result[good].(bandindx) = result[good].kcorr_mrest[iband] - lf_distmod(redshift[good],$
            omega0=omega0,omegal0=omegal0) - 5.0*alog10(1.0/h100) 
       endfor
       
    endif 

; write out    
    
    if keyword_set(write) then begin

       splog, 'Writing '+path+'mass_05savaglio_kcorr.fits.gz'
       mwrfits, result, path+'mass_05savaglio_kcorr.fits', /create
       spawn, ['gzip -f '+path+'mass_05savaglio_kcorr.fits'], /sh
       
    endif

return
end
