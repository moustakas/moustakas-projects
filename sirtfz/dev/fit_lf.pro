pro fit_lf, postscript=postscript
;+
; NAME:
;	FIT_LF
;
; PURPOSE:
;	Fit the 60mu IRAS luminosity function from Guiderdoni et al
;	1998 with a simple polynomial in order to increase the binsize
;	and to extrapolate the luminosity function to smaller
;	luminosities (~10^6).
;
; INPUTS:
;
; OUTPUTS:
;	Writes a two-column text file in the data subdirectory
;	containing the new luminosity function.	
;
; OPTIONAL OUTPUTS:
;	Postscript output of the fitted luminosity function.
;
; PROCEDURES USED:
;	READCOL, PLOTFAVES, COLORTABLE1, PS_OPEN, PS_CLOSE,
;	TEXSTRINGS(), SIRTF_DATAPATH()
;
; MODIFICATION HISTORY:
;	John Moustakas, 2000 September 30, U of A
;-

    path = (sirtf_datapath())[0]

; read in the 60mu luminosity function and plot it
    
    path = (sirtf_datapath())[0]
    readcol, path+'data/fir_lf.dat', lum, phi, skip=4, /silent, format='D,D'

    phi = alog10(10D^(phi)*2.5/(alog(10)*10D^(lum))) ; log number/Mpc^3/nu*L_nu (L_sun)

    plotfaves
    colortable1
    s = texstrings()
    plotsym, 0, 0.8, /fill

    if keyword_set(postscript) then begin
       ps_open, path+'plots/60mu_lf', /portrait
       device, /times, /inches
    endif else window, 0, xs=450, ys=450

    plot, lum, phi, ps=8, color=5, xsty=3, ysty=3, xrange=[6,12.5], $
      yrange=[-20,-7], xtit='log '+s.nu+textoidl('L_{\nu}')+'/'+textoidl('L_{sun}'), $
      ytit='log '+s.cphi+' '+textoidl('Mpc^{-3}')+' ('+s.nu+textoidl('L_{\nu}')+'/'+$
      textoidl('L_{sun}')+')'+textoidl('^{-1}'), tit='60 '+s.mu+'m Luminosity Function'

; fit a simple polynomial

    xlum = findgen((12.5-6.5)*50.)/50.+6.5
    coef = poly_fit(lum,phi,3,/double)
    fit = poly(xlum,coef)
    oplot, xlum, fit, line=0, color=2, thick=2

    if keyword_set(postscript) then ps_close

; write the fit to a file

    openw, lun, path+'data/fir_lf_fit.dat', /get_lun
    printf, lun, '# '+systime()
    printf, lun, '# 60mu fitted LF generated by FIT_LF'
    printf, lun, '# columns: (1) log nu*L_nu/L_sun; (2) log N Mpc^-3 nu*L_nu/L_sun'
    printf, lun, '#'
    for k = 0L, n_elements(xlum)-1L do printf, lun, xlum[k], fit[k], format='(1x,2F9.3)'
    free_lun, lun
          
    plotfaves, /restore

return
end
